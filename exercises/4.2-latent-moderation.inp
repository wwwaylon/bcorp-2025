TITLE: Latent Moderation

DATA: FILE = mydata2.dat;

VARIABLE: 
NAMES = ID Age PTSD1 PTSD2 PTSD3 SUP1 SUP2 SUP3 BCOPE1 BCOPE2 BCOPE3 Reint; 

MISSING = ALL (-999); 

USEVARIABLES = PTSD1 PTSD2 PTSD3 
               SUP1 SUP2 SUP3 
               BCOPE1 BCOPE2 BCOPE3;

ANALYSIS:
   TYPE = GENERAL RANDOM;
   ESTIMATOR = ML;
   ALGORITHM = INTEGRATION;

MODEL:
Z by PTSD1* PTSD2 PTSD3 ;
Z@1;
X by BCOPE1* BCOPE2 BCOPE3 ;
X@1;
Y by SUP1* SUP2 SUP3 ;
Y@1;

   XZ | X XWITH Z;

   Y ON X (b1);
   Y ON Z (b2);
   Y ON XZ (b3);

MODEL CONSTRAINT:
   NEW(LOW_Z MED_Z HIGH_Z SIMP_LO SIMP_MED SIMP_HI);

   LOW_Z = 0;   
   MED_Z = 1.5;   
   HIGH_Z = 3;   

   SIMP_LO = b1 + b3*LOW_Z;
   SIMP_MED = b1 + b3*MED_Z;
   SIMP_HI = b1 + b3*HIGH_Z;

   PLOT(LOMOD MEDMOD HIMOD);
   LOOP(XVAL,1,4,0.5);
   LOMOD = (b1 + b3*LOW_Z)*XVAL;
   MEDMOD = (b1 + b3*MED_Z)*XVAL;
   HIMOD = (b1 + b3*HIGH_Z)*XVAL;

PLOT: TYPE = plot2;
OUTPUT: CINT STAND;
